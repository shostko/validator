apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
}

artifacts {
    archives androidSourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Validator - library for easy validation'
                    url = 'https://github.com/shostko/validator'
                    licenses {
                        license {
                            name = 'Apache 2.0 License'
                            url = 'https://github.com/shostko/validator/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 's.shostko'
                            name = 'Sergey Shostko'
                            email = 's.shostko@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/shostko/validator.git'
                        developerConnection = 'scm:git:ssh://github.com/shostko/validator.git'
                        url = 'https://github.com/shostko/validator/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
